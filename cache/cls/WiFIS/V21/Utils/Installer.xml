<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="WiFIS.V21.Utils.Installer">
<Description>
Instalador WiFIS Easy Connect</Description>
<Super>%RegisteredObject</Super>
<TimeCreated>63481,41148.501632</TimeCreated>

<Parameter name="SrcVer">
<Description>
Location and Revision of this file in Perforce (Auto-updating) </Description>
<Default>$Id: //custom_ccrs/_common/tools/Iberia/WiFIS/latest/cls/WiFIS/V21/Utils/Installer.xml#1 $</Default>
</Parameter>

<Property name="InstallPath">
<Description>
Path to installation files</Description>
<Type>%String</Type>
<Parameter name="MAXLEN"/>
</Property>

<Method name="Run">
<Description>
Instalaci贸n de WiFIS Easy Connect + ITB.
pPath - Path a ficheros instalaci贸n WiFISEasyConnect.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>pPath:%String=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set ret = $$$OK
	set retTxt = "OK"
	set obj = ..%New()
	try {
		do obj.Log("WiFISEasyConnect Installer started")
		set obj.InstallPath=pPath
		$$$THROWONERROR(tSC, obj.Install())
		$$$THROWONERROR(tSC, obj.Check())
	} catch ex {
		set ret = ex.AsStatus()
		set retTxt = $system.Status.GetOneErrorText(ret)
	}
	do obj.Log("WiFISEasyConnect Installer ended")
	do obj.Log("Status="_retTxt)
	quit ret
]]></Implementation>
</Method>

<Method name="Install">
<Description>
Instalaci贸n</Description>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set ret = $$$OK
	try {
		set errorlog=""
		
		do ..Log("Install Files started")
		
		// normalize path
		if ..InstallPath="" $$$ThrowStatus($$$ERROR($$$GeneralError,"WiFIS path cannot be empty"))
		set wifis = ##class(%File).NormalizeDirectory("", ..InstallPath)
		if wifis="" $$$ThrowStatus($$$ERROR($$$GeneralError,"WiFIS path incorrect"))
		
		// inc
		set path = ##class(%File).SubDirectoryName(wifis,"inc",1)
		if path="" $$$ThrowStatus($$$ERROR($$$GeneralError,"WiFIS inc path incorrect"))
		do ..Log("__Importing includes...",1,0)
		do $system.OBJ.ImportDir(path, "*.inc", "/compile=1/display=none", .errorlog, 1, .imported, 0, .selectedlist)
		do ..Log("done",0,1)
		
		// check errors
		if +errorlog>0 {
			for i=1:1:errorlog do ..Log(errorlog(i),0,1)
			$$$ThrowStatus($$$ERROR($$$GeneralError,"Errors detected while importing includes"))
		}
		
		// cls
		set path = ##class(%File).SubDirectoryName(wifis,"cls",1)
		if path="" $$$ThrowStatus($$$ERROR($$$GeneralError,"WiFIS cls path incorrect"))
		do ..Log("__Importing classes...",1,0)
		do $system.OBJ.ImportDir(path, "*.xml", "/compile=1/display=none", .errorlog, 1, .imported, 0, .selectedlist)
		do ..Log("done",0,1)
		
		// check errors
		if +errorlog>0 {
			for i=1:1:errorlog do ..Log(errorlog(i),0,1)
			$$$ThrowStatus($$$ERROR($$$GeneralError,"Errors detected while importing classes"))
		}
		
		// schema
		set path = ##class(%File).SubDirectoryName(wifis,"schema",1)
		if path="" $$$ThrowStatus($$$ERROR($$$GeneralError,"WiFIS schema path incorrect"))
		do ..Log("__Importing hl7 schema...",1,0)
		do $system.OBJ.ImportDir(path, "*.xml", "/compile=1/display=none", .errorlog, 1, .imported, 0, .selectedlist)
		do ..Log("done",0,1)
		
		// check errors
		if +errorlog>0 {
			for i=1:1:errorlog do ..Log(errorlog(i),0,1)
			$$$ThrowStatus($$$ERROR($$$GeneralError,"Errors detected while importing classes"))
		}
		
		// lookup
		set path = ##class(%File).SubDirectoryName(wifis,"backup",1)_"hl7_WIFIS.Lookup.xml"
		if path="" $$$ThrowStatus($$$ERROR($$$GeneralError,"WiFIS hl7_WIFIS.Lookup.xml path incorrect"))
		do ..Log("__Importing lookup tables...",1,0)
		$$$THROWONERROR(tSC, ##class(Ens.Util.LookupTable).%Import(path))
		do ..Log("done",0,1)
		
		// check errors
		if +errorlog>0 {
			for i=1:1:errorlog do ..Log(errorlog(i),0,1)
			$$$ThrowStatus($$$ERROR($$$GeneralError,"Errors detected while importing classes"))
		}
		
		do ..Log("Install Files ended")
	} catch ex {
		set ret = ex.AsStatus()
	}
	quit ret
]]></Implementation>
</Method>

<Method name="Check">
<Description>
Comprobaci贸n WiFIS</Description>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set ret = $$$OK
	try {
		do ..Log("WiFISEasyConnect Checking started")
		
		set info = "WiFIS.Info"
		set exist = ##class(%Dictionary.ClassDefinition).%ExistsId(info)
		if 'exist $$$ThrowStatus($$$ERROR($$$GeneralError,info_" does not exist!"))
		do ..Log("__"_info_".VERSION="_$parameter(info,"VERSION"))
		
		set itb = "ITB.Info"
		set itbversion = $parameter(itb,"VERSION")
		set itbmin=$parameter(info,"ITBMINVERSION")
		set ok = (itbversion >= itbmin)
		do ..Log("__"_info_".ITBMINVERSION="_itbmin)
		
		if 'ok $$$ThrowStatus($$$ERROR($$$GeneralError,"ITBMINVERSION="_itbmin_" and ITBVERSION="_itbversion))
		
		do ..Log("WiFISEasyConnect Checking ended")
	} catch ex {
		set ret = ex.AsStatus()
	}
	quit ret
]]></Implementation>
</Method>

<Method name="Log">
<Description>
Log a message
pText - Text message
pTimeStamp - Add a timestamp
pNewLine - Add a new line</Description>
<FormalSpec>pText:%String,pTimeStamp:%Boolean=1,pNewLine:%Boolean=1</FormalSpec>
<Implementation><![CDATA[	write $case(pTimeStamp,1:"["_$zdt($h,3)_"] ",:"")_pText_$case(pNewLine,1:$$$NL,:"")
]]></Implementation>
</Method>
</Class>
</Export>
